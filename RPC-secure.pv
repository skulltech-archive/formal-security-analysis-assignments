(*
   A -> B: msg
   B -> A: f(msg)
*)

free ch: channel.

type skey.
type pkey.
fun pk(skey):pkey.

fun nmsg(bitstring, bitstring): bitstring [data]. 
fun smsg(bitstring, bitstring): bitstring [data].

fun penc(pkey,bitstring): bitstring.
reduc forall sk:skey, m:bitstring;
      pdec(sk,penc(pk(sk),m)) = m.

fun sign(skey,bitstring): bitstring.
reduc forall sk:skey, m:bitstring;
      verify(pk(sk),m,sign(sk,m)) = true.


free msg:bitstring [private].
fun f(bitstring):bitstring [private].

(* Secrecy of msg and f(msg): SHOULD BE TRUE *)
query attacker(msg).
query attacker(f(msg)).

(* Request Authentication: SHOULD BE TRUE *)
event SendA(bitstring).
event RecvB(bitstring).
query m:bitstring; event(RecvB(m)) ==> event(SendA(m)).

(* Response Authentication: SHOULD BE TRUE *)
event SendB(bitstring,bitstring).
event RecvA(bitstring,bitstring).
query req:bitstring,resp:bitstring;
      event(RecvA(req,resp)) ==> event(SendB(req,resp)).

(* Reachability Queries: sanity checks SHOULD BE FALSE *)

query req:bitstring; event(SendA(req)).
query req:bitstring; event(RecvB(req)).
query req:bitstring, resp:bitstring; event(SendB(req,resp)).
query req:bitstring, resp:bitstring; event(RecvA(req,resp)).


(* A -> B: m *)
let A(skA:skey,pkB:pkey,req:bitstring) =
  in(ch, enmsg:bitstring);
  let nc = pdec(skA, enmsg) in
  event SendA(req);
  out(ch, smsg(penc(pkB, nmsg(nc, req)), sign(skA, penc(pkB, nmsg(nc, req)))));
  in(ch, smsg(enres, sig));
  if verify(pkB, enmsg, sig) then
  let nmsg(=nc, res) = pdec(skA, enres) in
  event RecvA(req, res).


(* B -> A: f(m) *)
let B(skB:skey,pkA:pkey) =
  new nc: bitstring;
  out(ch, penc(pkA, nc));
  in(ch, smsg(enmsg, sig));
  if verify(pkA, enmsg, sig) then
  let nmsg(=nc, req) = pdec(skB, enmsg) in
  event RecvB(req);
  event SendB(req, f(req));
  out(ch, smsg(penc(pkA, nmsg(nc, f(req))), sign(skB, penc(pkA, nmsg(nc, f(req)))))).


process
  new skA: skey;
  new skB: skey;
  let pkA = pk(skA) in
  let pkB = pk(skB) in
  out(ch,(pkA,pkB));
  A(skA,pkB,msg) | B(skB,pkA)
